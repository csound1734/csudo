/****************************************************************************
ifno TbScala Sfil [, ibasefreq [, ibasekey [, ifn]]] 
Creates a table by reading a scala scale file (.scl)

Scale values are converted to decimal format useable by the cpstun(i) opcode.
Written by Thorin Kerr

ifno - Return number of generated table.
Sfil - scale file name (or path) in double quotes
ibasefreq - The base frequency of the scale in cycles per second (default = Csounds A4 value)
ibasekey - The integer index of the scale to which to assign basefreq unmodified (default = 69)
ifn - destination table number of the table. (default 0: generated by Csound)

The scala software, file format, and scale archive is available from  http://www.huygens-fokker.org/scala/
****************************************************************************/

<CsoundSynthesizer>
<CsOptions>
-odac --nchnls=2 --0dbfs=1 --nodisplays --sample-rate=44100 --ksmps=32 -m128
</CsOptions>
<CsVersion>
After 6.09
</CsVersion>
<CsInstruments>

opcode TbScala, i, Sjjo
 ScalaFile, ibasefreq, ibasekey, ifn xin
 if filevalid(ScalaFile) == 0 then
  prints "TBScala didn't work. File not found\n"
  idummy_ = 0
 else
  iline init 0
  inoncommentline init 0
  inumgrades init 0
  ipitwrites init 0
  ibasefreq = (ibasefreq == -1 ? A4 : ibasefreq)
  ibasekey = (ibasekey == -1 ? 69 : ibasekey)
  while iline != -1 do
   Sline, iline readfi ScalaFile
   istrlen strlen Sline
   if strchar:i(Sline) == 33 then
    ;;ignore commentlines
   elseif inoncommentline == 0 then
    ;;; first non comment line is description
    inoncommentline += 1
   elseif inoncommentline == 1 then
    ;;; second non comment line is numnotes
    inumgrades strtod strsub(Sline, 0, istrlen - 1)
    ipitstore[] init inumgrades
    inoncommentline += 1
   else
    ;; cents or ratio's from here
    ipos init 0
    istartslice init -1
    iendslice init -1
    icentformat init 0
    while ipos < istrlen do
     ichr strchar Sline, ipos
     if (ichr >= 45 && ichr < 58) then
      if (istartslice == -1) then
       istartslice = ipos
      endif
      if (ichr == 46) then
       icentformat = 1
      endif
     elseif (istartslice >= 0 && iendslice == -1) then
      iendslice = ipos
      igoto breakout
     endif
     ipos += 1
    od
    breakout:
    if (istartslice != -1) then
     Ssegment strsub Sline, istartslice, iendslice
     if (icentformat == 1) then
      ipitval strtod Ssegment
     else
      idiv strindex Ssegment, "/"
      Snumer strsub Ssegment, 0, idiv
      Sdenom strsub Ssegment, idiv+1
      inumer strtod Snumer
      idenom = (idiv == -1 ? 1 : strtod:i(Sdenom)) 
      ipitval = inumer/idenom
     endif            
     ipitval = (icentformat == 1 ? (2 ^ (ipitval/1200)) : ipitval)
     ipitstore[ipitwrites] = ipitval
     ipitwrites += 1
    endif
   endif
  od
  idummy_ ftgen ifn, 0, -(ipitwrites + 5), -2, inumgrades, ipitval, ibasefreq, ibasekey, 1
  iarrstep init 0
  while iarrstep < inumgrades do
   tablew ipitstore[iarrstep], iarrstep + 5, idummy_
   iarrstep += 1
  od
 endif
 xout idummy_
endop 

;change this to where your scale file is located
giScale TbScala "../../_sourcefiles/carlos_alpha.scl"

instr 1
 ihz = cpstuni(p5, giScale)
 kfilt expseg 8000, p3*0.2, ihz*2, p3*0.8, ihz*8
 ares vco2 p4, ihz
 ares mvclpf2 ares, kfilt, 0.1;xres[,istor]
 ares *= linseg(0,0.01,1,p3-0.1,1,0.05,0) ;declick
 ares *= lineto(expcurve((1/active:k(p1)),0.11), 0.01) ;amp control
 outs ares, ares
endin

</CsInstruments>
<CsScore>
i 1 0      [4-0]    0.8 50
i . ^+0.5  [4-0.5]  0.8 55
i . ^+0.4  [4-0.9]  .   59
i . ^+0.35 [4-1.25] .   65
i . ^+0.35 [4-1.6]  .   70
i . ^+0.4  [4-2]    .   74
i . ^+2.2  [4-0]    .   73
i . ^+0.2  [4-0.2]  .   69
i . ^+0.2  [4-0.4]  .   65
i . ^+0.2  [4-0.6]  .   61
i . ^+0.2  [4-0.8]  .   57
i . ^+0.2  [4-1]    .   53
i . ^+3    [7-0]    .   62
i . ^+0.1  [7-0.1]  .   67
i . ^+0.1  [7-0.2]  .   71
i . ^+0.2  [7-0.4]  .   40
i . ^+0.4  [7-0.8]  .   31
i . 5      1        .   80
i . +      .        .   79
i . +      .        .   78
i . +      0.5      .   77
i . +      .        .   75
i . +      0.25     .   77
i . +      .        .   75
i . +      0.75     .   77
</CsScore>
</CsoundSynthesizer>
<bsbPanel>
 <label>Widgets</label>
 <objectName/>
 <x>100</x>
 <y>100</y>
 <width>320</width>
 <height>240</height>
 <visible>true</visible>
 <uuid/>
 <bgcolor mode="nobackground">
  <r>255</r>
  <g>255</g>
  <b>255</b>
 </bgcolor>
</bsbPanel>
<bsbPresets>
</bsbPresets>
